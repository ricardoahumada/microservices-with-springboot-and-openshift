pipeline {
    agent any

    parameters {
        choice(name: 'ENVIRONMENT', 
               choices: ['dev', 'qa', 'prod'], 
               description: 'Target environment for deployment')
        booleanParam(name: 'SKIP_TESTS', 
                     defaultValue: false, 
                     description: 'Skip unit tests')
    }

    environment {
        APP_NAME = 'afiliado-service'
        APP_VERSION = ''
        DOCKERHUB_REPO = 'ricardoahumada/afiliado-service'
        OPENSHIFT_PROJECT = "ricardoag-${params.ENVIRONMENT}"
    }

    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
        timeout(time: 30, unit: 'MINUTES')
        timestamps()
        disableConcurrentBuilds()
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
                script {
                    // Detectar rama actual (quitar prefijo origin/)
                    env.BRANCH_NAME = env.GIT_BRANCH?.replaceFirst(/^origin\//, '') ?: 'unknown'
                    env.GIT_COMMIT_SHORT = sh(script: 'git rev-parse --short HEAD', returnStdout: true).trim()
                    echo "GIT_BRANCH: ${env.GIT_BRANCH}"
                    
                    // Leer version del pom.xml
                    def pom = readMavenPom file: 'afiliado-service/pom.xml'
                    def version = pom.version ?: pom.parent?.version ?: '1.0.0'
                    def appVersion = version.replace('-SNAPSHOT', '')
                    env.APP_VERSION = appVersion
                    echo "APP_VERSION: ${appVersion}"
                    
                    // GitFlow: Generar IMAGE_TAG segun la rama
                    if (env.BRANCH_NAME == 'main' || env.BRANCH_NAME == 'master') {
                        env.IMAGE_TAG = appVersion
                    } else if (env.BRANCH_NAME.startsWith('release/')) {
                        def releaseVersion = env.BRANCH_NAME.replace('release/', '')
                        env.IMAGE_TAG = "rc-${releaseVersion}-${BUILD_NUMBER}"
                    } else if (env.BRANCH_NAME.startsWith('hotfix/')) {
                        def hotfixVersion = env.BRANCH_NAME.replace('hotfix/', '')
                        env.IMAGE_TAG = "hotfix-${hotfixVersion}-${BUILD_NUMBER}"
                    } else if (env.BRANCH_NAME == 'develop') {
                        env.IMAGE_TAG = "develop-${BUILD_NUMBER}"
                    } else {
                        // feature/* u otras ramas
                        env.IMAGE_TAG = "dev-${GIT_COMMIT_SHORT}"
                    }
                    
                    echo "Branch: ${env.BRANCH_NAME}"
                    echo "Image Tag: ${env.IMAGE_TAG}"
                }
            }
        }

        stage('Build & Test') {
            steps {
                dir('afiliado-service') {
                    sh """
                        echo "Building ${APP_NAME}..."
                        mvn clean package -DskipTests=${params.SKIP_TESTS}
                    """
                }
            }
            post {
                always {
                    junit allowEmptyResults: true, testResults: '**/target/surefire-reports/*.xml'
                }
            }
        }

        stage('Docker Build & Push') {
            steps {
                withCredentials([usernamePassword(credentialsId: 'dockerhub-credentials', 
                                                  usernameVariable: 'DOCKER_USER', 
                                                  passwordVariable: 'DOCKER_PASS')]) {
                    dir('afiliado-service') {
                        sh """
                            echo "Logging in to DockerHub..."
                            echo \${DOCKER_PASS} | docker login -u \${DOCKER_USER} --password-stdin
                            
                            echo "Building Docker image: ${DOCKERHUB_REPO}:${IMAGE_TAG}"
                            docker build -t ${DOCKERHUB_REPO}:${IMAGE_TAG} .
                            
                            echo "Pushing to DockerHub..."
                            docker push ${DOCKERHUB_REPO}:${IMAGE_TAG}
                            
                            echo "Push completed successfully"
                        """
                    }
                }
            }
        }

        stage('Tag Latest') {
            when {
                expression { env.BRANCH_NAME == 'main' || env.BRANCH_NAME == 'master' }
            }
            steps {
                withCredentials([usernamePassword(credentialsId: 'dockerhub-credentials', 
                                                  usernameVariable: 'DOCKER_USER', 
                                                  passwordVariable: 'DOCKER_PASS')]) {
                    sh """
                        echo "Tagging as latest..."
                        docker tag ${DOCKERHUB_REPO}:${IMAGE_TAG} ${DOCKERHUB_REPO}:latest
                        docker push ${DOCKERHUB_REPO}:latest
                    """
                }
            }
        }

        stage('Deploy to OpenShift') {
            steps {
                withCredentials([string(credentialsId: 'openshift-token', variable: 'OC_TOKEN')]) {
                    script {
                        def valuesFile = "values-${params.ENVIRONMENT}.yaml"
                        sh """
                            echo "Logging in to OpenShift..."
                            oc login --token=\${OC_TOKEN} --server=\${OPENSHIFT_SERVER} --insecure-skip-tls-verify=true
                            
                            echo "Deploying to ${OPENSHIFT_PROJECT} with ${valuesFile}..."
                            cd charts/afiliado-service-chart
                            
                            echo "Building chart dependencies..."
                            helm dependency build
                            
                            helm upgrade --install ${APP_NAME} . \\
                                --namespace ${OPENSHIFT_PROJECT} \\
                                --values ${valuesFile} \\
                                --set image.tag=${IMAGE_TAG} \\
                                --set image.repository=${DOCKERHUB_REPO} \\
                                --wait \\
                                --timeout 10m \\
                                --atomic
                            
                            echo "Deployment completed successfully"
                        """
                    }
                }
            }
        }

        stage('Verify Deployment') {
            steps {
                sh """
                    echo "Verifying deployment..."
                    helm status ${APP_NAME} -n ${OPENSHIFT_PROJECT}
                    
                    echo "Checking pod status..."
                    oc get pods -n ${OPENSHIFT_PROJECT} -l app.kubernetes.io/name=${APP_NAME}
                """
            }
        }

        stage('Production Approval') {
            when {
                expression { params.ENVIRONMENT == 'prod' }
            }
            steps {
                input message: 'Deploy to Production?', 
                      ok: 'Approve',
                      submitter: 'admin,devops-lead'
            }
        }
    }

    post {
        success {
            echo "Pipeline completed successfully for ${APP_NAME} v${IMAGE_TAG} (branch: ${env.BRANCH_NAME})"
        }
        failure {
            echo "Pipeline failed for ${APP_NAME}"
        }
        always {
            cleanWs()
        }
    }
}
